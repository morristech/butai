apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.novoda.bintray-release'

ext {
    versionFile = project.file("VERSION")
    versionText = versionFile.readLines()[0].trim()
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName versionText

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testCompile 'junit:junit:4.12'
}

publish {
    userOrg = 'shiraji'
    groupId = GROUPID
    artifactId = ARTIFACTID
    version = versionText
    description = 'Extend classes that have the status the app is foreground/background/just appearing'
    website = 'https://github.com/shiraji/butai'
    licences = ['Apache-2.0']
}

task javadoc(type: Javadoc) {
    excludes = ['**/*.kt']
}

tasks.withType(Javadoc) {
    options.addBooleanOption('Xdoclint:none', true)
}

task prepareForNextDevelopment << {
    def versionParts = versionText.split(/\./)
    versionParts[2] = (versionParts[2] as int) + 1
    def newVersion = "${versionParts[0]}.${versionParts[1]}.${versionParts[2]}-SNAPSHOT"
    versionFile.write(newVersion + "\n")
}

task prepareForRelease << {
    def versionParts = versionText.split(/\./)
    versionParts[2] = versionParts[2].split(/-/)[0]
    def newVersion = "${versionParts[0]}.${versionParts[1]}.${versionParts[2]}"
    versionFile.write(newVersion + "\n")
    new File("$projectDir/../.travis/release").createNewFile()
}
